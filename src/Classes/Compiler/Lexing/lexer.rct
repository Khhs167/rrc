// Compiler/Lexing/lexer in rcc
// Yea, this lexes :)

////// PREDEFINITIONS //////
set pointer <- 0;
set tokens <- make Token array(0);


////// MAIN TOKEN BIT //////
function Lex() {
    while(!EOF()){

        // Ok, do checking here ig //

        var pos <- GetPos(pointer, code);

        // whitespaces
        if(Current() = " ")  { Step(1); continue; }
        if(Current() = "\t") { Step(1); continue; }
        if(Current() = "\r") { Step(1); continue; }
        if(Current() = "\n") { tokens->Push(make Token(TokenType->Newline, "", pos)); Step(1); continue; }
        if(Current() = ";")  { tokens->Push(make Token(TokenType->Newline, "", pos)); Step(1); continue; }

        //expression characters
        if(Current() = "(") { tokens->Push(make Token(TokenType->OpenParenthasis, "", pos));     Step(1); continue; }
        if(Current() = ")") { tokens->Push(make Token(TokenType->CloseParenthasis, "", pos));    Step(1); continue; }
        if(Current() = ",") { tokens->Push(make Token(TokenType->Comma, "", pos));               Step(1); continue; }

        if (IsLetter(Current())) { var word <- LexWord(); var wordType <- LexWordType(word); tokens ->Push(make Token(wordType, word, pos)); continue; }

        //lets keep it simple for now. -rc
        //if(Current() = ":" && Peek(1) = ":") { tokens->Push(GenToken(TokenType->PackageAcs, "")); pointer++; pointer++; continue;}

        if (Current() = """") { tokens ->Push(make Token(TokenType ->String, LexString(), pos)); pointer++; continue; }

        //Print error, noice
        ReportError("Unexpected character: """ + Current() + """", ErrorType->InvalidCharacterError, GetPos(pointer, code));

        //Increase pointer, dont get stuck
        Step(1);
    }
}

function PrintError(error string, errortype ErrorType){
        SetConsoleForeground(4);
        Print("ERROR: " + string(errortype) + ": " + error);
        SetConsoleForeground(7);
}

function LexWordType(token string) TokenType{
    if(ContainsItem(keywords, token)){
        return TokenType->Keyword
    }
    return TokenType->Word;
}
function LexWord() string{
    var word <- "";
    while(IsLetter(Current()) && !EOF()){
        word <-+ Current();
        pointer++;
    }
    return word;
}

function LexString() string{
    var str <- "";
    pointer++
    while(Current() != Char(34) && !EOF()){
        str <-+ Current();
        pointer++;
    }
    return str;
}



// Here we define functions to get deeta //
function Current() string { return Peek(0); }                                                                       // Current char in code
function Peek(offset int) string { if (EOFO(offset)) return ""; return code ->Substring(pointer + offset, 1); }     // Peek into the future
function EOF() bool { return pointer >= code ->GetLength(); }                                                       // Is it EOF at current?
function EOFO(offset int) bool { return pointer + offset >= code ->GetLength(); }                                   // Is it EOF in [offset] chars
function Step(amount int) { pointer <-+ amount; }                                                                   // QOL function